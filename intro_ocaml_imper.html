<mychapter id="progimperative" title="Programmation impérative">
<p><cmp>À la différence de la programmation fonctionnelle où on calcule une valeur
par l'application d'une fonction à ses arguments sans se soucier du déroulement
des opérations, la programmation impérative est plus proche de la représentation
machine car elle introduit un état mémoire que le déroulement des actions
d'un programme va modifier. On appelle instructions ces actions des programmes
et un programme impératif est une suite, ou une séquence, d'instructions.
L'état mémoire est susceptible d'être modifié à l'exécution de chaque
instruction. On considère les opérations d'entrées-sorties comme des modifications
de la mémoire, de la mémoire vidéo ou de fichiers.</cmp>
</p>
<p>
<cmp>Ce style de programmation est directement inspiré de la programmation assembleur.
On le retrouve dans les premiers langages évolués généralistes (Fortran, C, Pascal,
etc. ). En OCaml, les éléments suivants du langage correspondent à ce modèle:</cmp>
</p>
<ul>
<li><cmp>les structures de données physiquement modifiables, comme les vecteurs ou les
enregistrements à champs modifiables,</cmp></li>
<li><cmp>les opérations d'entrées-sorties,</cmp></li>
<li><cmp>les structures de contrôle de l'exécution comme les boucles</cmp>, voire les exceptions
qui peuvent être considérées de style impératif.</li>
</ul>
<p>
<cmp>Certains algorithmes s'écrivent plus facilement dans ce style de programmation. On peut
citer comme exemple le produit de deux matrices. Même s'il est effectivement possible de le
traduire dans une version purement fonctionnelle, où des listes remplacent les vecteurs,
cela n'est ni naturel, ni efficace par rapport à une écriture impérative.</cmp>
</p>
<p>
<cmp>L'intérêt d'intégrer ce modèle dans un langage fonctionnel est de pouvoir écrire
certains algorithmes dans ce style de programmation quand ceux-ci s'y prêtent. Les deux
principaux désavantages par rapport au style purement fonctionnel sont:</cmp>
</p>
<ul>
<li><cmp>de compliquer le système de types du langage et de rejeter certains programmes
qui sans cela seraient considérés comme corrects,</cmp></li>
<li><cmp>de devoir tenir compte de la représentation mémoire et de l'ordre des calculs.</cmp></li>
</ul>
<p>
<cmp>Néanmoins, avec quelques règles de prudence dans l'écriture des programmes, le choix
entre plusieurs styles de programmation offre de plus grandes possibilités d'écriture
d'algorithmes, ce qui est l'objectif principal des langages de programmation. En
outre, un programme écrit dans un style proche de l'algorithme utilisé sera plus simple
donc aura plus de chances d'être correct (ou, tout du moins, plus rapidement mis au point).</cmp>
</p>
<p>
<cmp>Pour ces raisons, le langage OCaml possède des types de données dont les valeurs sont
physiquement modifiables, des structures de contrôle de l'exécution des programmes et une
bibliothèque d'entrées-sorties dans un style impératif.</cmp>
</p>
<section id="imper:structs" title="Structures de données physiquement modifiables">
<p>
La programmation impérative nécessite des structures de données modifiables. OCaml en
est pourvu et nous les détaillons ci-dessous.
</p>
<subsection id="vectors" title="Vecteurs">
<p>
Les vecteurs sont des tableaux à une dimension, de taille fixe, et contenant des éléments
qui sont tous du même type. Ils sont le pendant impératif des listes. Le type OCaml
pour les représenter est le type paramétré <ml>'a array</ml>, le paramètre de type
<ml>'a</ml> permettant d'avoir des vecteurs de différents types, de la même manière
que le type <ml>'a list</ml>.
</p>

<subsubsection id="vectors:create" title="Construction de valeurs">
<p>
La construction d'un vecteur se fait en listant, séparées par des '<ml>;</ml>',
les valeurs du vecteur entre <ml>[|</ml> et <ml>|]</ml>:
</p>
<ocaml-eval toplevel="true">let tab = [| 1 ; 2 ; 3 ; 4 |];;</ocaml-eval>
<p>
Il est également possible d'utiliser des fonctions du module <moduledoc name="Array"/>
pour créer des vecteurs:
</p>
<ocaml-eval toplevel="true"><![CDATA[
let tab = Array.create 10 0;;
let tab = Array.init 10 (fun i -> i * 2);;
]]></ocaml-eval>
<p>
Les indices d'un vecteur de longueur n vont de 0 à n-1.
</p>
</subsubsection>

<subsubsection id="vectors:access" title="Accès">
<p>
On peut accéder de deux façons équivalentes à un élément d'un vecteur:
</p>
<ul>
<li>soit en utilisant la fonction <ml>Array.get : 'a array -> int -> 'a</ml>:
<ocaml-eval toplevel="true"><![CDATA[
Array.get tab 0;;
Array.get tab 3;;
]]></ocaml-eval></li>
<li>soit en utilisant la syntaxe <ml>vecteur.(indice)</ml>:
<ocaml-eval toplevel="true"><![CDATA[
tab.(0);;
tab.(3);;
]]></ocaml-eval></li>
</ul>
<p>
L'accès à un élément d'un vecteur de longueur n en utilisant un indice en
dehors de la plage 0..n-1 déclenche la levée d'une exception <ml>Invalid_argument</ml>:
</p>
<ocaml-eval toplevel="true" error-exc="false">tab.(42);;</ocaml-eval>
<p>
On peut bien sûr également utiliser le filtrage sur les vecteurs:
</p>
<ocaml-eval toplevel="true"><![CDATA[
match Array.sub tab 0 2 with
  [| _ ; x |] when x > 2 -> false
| [| 0 ; 2 |] -> true
| _ -> false;;
]]></ocaml-eval>
</subsubsection>

<subsubsection id="vectors:mod" title="Modification">
<p>
Il existe deux manières équivalentes de modifier un élément d'un vecteur:
</p>
<ul>
<li>soit en utilisant la fonction <ml>Array.set : 'a array -> int -> 'a -> unit</ml>:
<ocaml-eval toplevel="true"><![CDATA[
tab.(0);;
Array.set tab 0 42;;
tab.(0);;
]]></ocaml-eval></li>
<li>soit en utilisant la syntaxe <ml>vecteur.(indice) &lt;- valeur</ml>:
<ocaml-eval topleve="true"><![CDATA[
tab.(1);;
tab.(1) <- 42;;
tab.(1);;
]]></ocaml-eval></li>
</ul>
<p>
Comme pour l'accès, et en général toutes les fonctions agissant sur les vecteurs,
une exception <ml>Invalid_argument</ml> est levée lors de l'utilisation d'un indice
invalide (en dehors des bornes).
</p>
</subsubsection>
<subsubsection id="vectors:other" title="Autres opérations">
<p>
D'autres opérations sur les vecteurs sont disponibles via des fonctions prédéfinies
du module <moduledoc name="Array"/>:
</p>
<ocaml-eval toplevel="true"><![CDATA[
Array.length tab (* longueur *) ;;
Array.sub tab 3 4 (* extraction d'un sous-vecteur de longueur 4 a partir de l'indice 3 *);;
Array.to_list tab (* creation d'une liste a partir des elements du vecteur *);;
Array.map string_of_int tab (* map sur les elements du vecteur *) ;;
]]></ocaml-eval>
<p>
On consultera la documentation du module <moduledoc name="Array"/>.
</p>
</subsubsection>
</subsection>

<subsection id="strings_imper" title="Chaînes de caractères">
<p>
Les chaînes de caractères sont les valeurs de type <ml>string</ml>, comme nous en avons
déjà croisées plus haut. En OCaml, les chaînes sont mutables, à la façon des
vecteurs. Cependant, les chaînes ont leur type propre et ne sont pas des vecteurs
de caractères (le type <ml>string</ml> n'est pas unifiable au type <ml>char array</ml>).
</p>

<subsubsection id="strings:create" title="Création de valeurs">
<p>
On crée une chaîne de caractères en la mettant entre guillemets <ml>"</ml>:
</p>
<ocaml-eval toplevel="true">"hello world !";;</ocaml-eval>
<p>
Le caractère <ml>\</ml> est utilisé pour indiquer des caractères spéciaux:
</p>
<ul>
<li><ml>\"</ml> permet d'insérer le caractère " au lieu de signaler la
fin de la chaîne,</li>
<li><ml>\n</ml> indique une fin de ligne,</li>
<li><ml>\r</ml> indique un retour charriot,</li>
<li><ml>\t</ml> indique une tabulation.</li>
</ul>
<ocaml-eval toplevel="true"><![CDATA[
print_endline "\"Entre guillemets\"\nune nouvelle ligne";;
]]></ocaml-eval>
<p>
Il est également possible d'utiliser des fonctions créant des chaînes:
</p>
<ocaml-eval toplevel="true"><![CDATA[
let chaine = String.make 10 'a';;
string_of_int 42;;
string_of_float 84.;;
Printexc.to_string Not_found (* une chaine à partir d'une exception *);;
]]></ocaml-eval>
</subsubsection>

<subsubsection id="strings:access" title="Accès">
<p>
Comme pour les valeurs, on peut accéder à l'un des caractères d'une chaîne
de deux manières:
</p>
<ul>
<li>soit en utilisant la fonction <ml>String.get : string -> int -> char</ml>:
<ocaml-eval toplevel="true">String.get chaine 0;;</ocaml-eval></li>
<li>soit en utilisant la syntaxe <ml>chaine.[indice]</ml>:
<ocaml-eval toplevel="true">chaine.[0];;</ocaml-eval></li>
</ul>
<p>
Les indices pour une chaîne de longueur n vont de 0 à n-1. En dehors de ces bornes,
un accès déclenche là encore une exception <ml>Invalid_argument</ml>:
</p>
<ocaml-eval toplevel="true" error-exc="false">chaine.[10];;</ocaml-eval>
</subsubsection>

<subsubsection id="strings:mod" title="Modifications">
<p>
Il existe deux manières équivalentes de modifier un caractère d'une chaîne:
</p>
<ul>
<li>soit en utilisant la fonction <ml>String.set : string -> int -> char -> unit</ml>:
<ocaml-eval toplevel="true"><![CDATA[
chaine.[0];;
String.set chaine 0 'b';;
chaine.[0];;
]]></ocaml-eval></li>
<li>soit en utilisant la syntaxe <ml>chaine.[indice] &lt;- valeur</ml> avec <ml>valeur</ml>
de type <ml>char</ml> (caractère):
<ocaml-eval toplevel="true"><![CDATA[
chaine.[1];;
chaine.[1] <- 'c';;
chaine.[1];;
]]></ocaml-eval></li>
</ul>
</subsubsection>

<subsubsection id="strings:other" title="Autres opérations">
<p>
D'autres opérations sur les chaînes sont offertes par les fonctions du module
<moduledoc name="String"/>. Par exemple:
</p>
<ocaml-eval toplevel="true"><![CDATA[
String.length chaine (* longeur d'une chaine *);;
String.sub "hello world" 3 4 (* extraction de sous-chaine, comme pour Array.sub *);;
let animaux = [ "chat"; "chien"; "perroquet"; "lama"; "renard" ];;
String.concat ", " animaux (* concatenation d'une liste de chaines en utilisant un separateur *);;
]]></ocaml-eval>
<p>
Pour d'autres fonctions, on consultera la documentation du module
<moduledoc name="String"/>.
</p>
</subsubsection>
</subsection>

<subsection id="mutrecords" title="Champs modifiables des enregistrements">
<p>
Nous avons vu précédemment (cf. <elt href="#records"/>) comment définir des types enregistrements.
Il est possible de spécifier un ou plusieurs champs comme étant modifiables, à l'aide du
mot-clé <ml>mutable</ml>:
</p>
<ocaml-eval toplevel="true"><![CDATA[
type mon_record = {
  champ1 : int ;
  mutable champ2 : int ;
  };;
]]></ocaml-eval>
<p>
La création et le filtrage de valeurs de ce type ne changent pas, mais il est maintenant possible
de modifier le champ <ml>champ2</ml> de ces valeurs:
</p>
<ocaml-eval toplevel="true"><![CDATA[
let r = { champ1 = 0 ; champ2 = 1 };;
r.champ2 <- 2;;
r;;
]]></ocaml-eval>
<p>
Par contre, tenter de modifier le champ <ml>champ1</ml> provoque une erreur:
</p>
<ocaml-eval toplevel="true" error-exc="false"><![CDATA[r.champ1 <- 3;;]]></ocaml-eval>
</subsection>

<subsection id="refs" title="Références">
<p><cmp>OCaml fournit un type polymorphe <ml>'a ref</ml> qui peut être vu comme le type des pointeurs sur
une valeur quelconque~; en OCaml, on parlera plutôt de référence sur une valeur. Une valeur
référencée peut être modifiée. Le type <ml>ref</ml> est définit comme un enregistrement à un
champ modifiable:</cmp>
</p>
<ocaml>type 'a ref = {mutable contents:'a}</ocaml>
<p><cmp>Ce type est muni de raccourcis syntaxiques prédéfinis. On construit une référence
sur une valeur par la fonction <ml>ref</ml>. La valeur référencée peut être atteinte par la
fonction préfixe <ml>(!)</ml>.
La fonction modifiant le contenu d'une référence est la fonction infixe <ml>(:=)</ml>.</cmp>
</p>
<ocaml-eval toplevel="true"><![CDATA[
let x = ref 3 (* creation et initialisation *);;
x ;;
!x (* dereferencement *);;
x := 4 ;;
!x ;;
x := !x+1 (* affectation *);;
!x ;;
]]></ocaml-eval>
<p>
Remarque: Là encore, pour plus de sûrété, il n'est pas syntaxiquement possible de
créer une référence sans l'initialiser à une valeur, contrairement au langage C.
</p>
</subsection>
</section>

<section id="inout" title="Entrées-sorties">
<p><cmp>Les fonctions d'entrées-sorties calculent une valeur (souvent de type <ml>unit</ml>),
mais durant ce calcul elles effectuent une modification de l'état des périphériques
d'entrées-sorties: modification du buffer du clavier, affichage à l'écran, écriture
dans un fichier ou modification du pointeur de lecture. Les deux types suivants sont
prédéfinis: <ml>in_channel</ml> et <ml>out_channel</ml> pour respectivement les canaux
d'entrées et de sorties. Quand une fin de fichier est rencontrée l'exception
<ml>End_of_file</ml> est déclenchée. Enfin, les trois constantes suivantes correspondent
aux canaux standard d'entrée, de sortie et d'erreur à la manière d'Unix:
<ml>stdin</ml>, <ml>stdout</ml> et <ml>stderr</ml>.</cmp>
</p>

<subsection id="inout:channels" title="Canaux">
<p>
Pour obtenir d'autres canaux que ceux prédéfinis (<ml>stdin</ml>, <ml>stdout</ml> et <ml>stderr</ml>),
on utilise:
</p>
<ul>
<li>la fonction <ml>open_in: string -> in_channel</ml> qui ouvre un fichier existant en
lecture,</li>
<li>la fonction <ml>open_out: string -> out_channel</ml> qui ouvre un fichier en écriture,
écrasant le fichier s'il existe déjà ou le créant s'il n'existe pas.</li>
</ul>
<p>
Les problèmes rencontrés par ces fonctions sont signalés par une levée d'exception
<ml>Sys_error</ml> avec un message explicite, par exemple lorsqu'un fichier à ouvrir en lecture
n'existe pas, quand les droits ne sont pas suffisants, etc.
</p>
<ocaml-eval toplevel="true"><![CDATA[
let ic = open_in "/etc/passwd";;
let oc = open_out "/tmp/foo";;
let ic = open_in "/bar";;
]]></ocaml-eval>
<p>
Pour fermer un fichier en lecture ou en écriture, on utilisera respectivement
les fonctions <ml>close_in</ml> et <ml>close_out</ml>.
</p>
<p>
On notera que, grâce au typage, on ne pourra utiliser un canal d'entrée
(<ml>in_channel</ml>) pour y écrire des données, ni un canal de sortie
(<ml>out_channel</ml>) pour lire des données.
</p>
</subsection>

<subsection id="inout:read" title="Lecture">
<p>
Voici quelques fonctions de lecture sur un canal de type <ml>in_channel</ml>.
D'autres fonctions sont disponibles dans la
<ext-a href="&lt;urldocinputfuns/&gt;">bibliothèque de base</ext-a>.
</p>
<ocaml-eval toplevel="true"><![CDATA[
let first_line = input_line ic (* lecture d'une ligne *);;
let second_line = input_line ic (* lecture d'une ligne *);;
seek_in ic 0 (* retour au debut du fichier *);;
let line = input_line ic (* re-lecture de la premiere ligne, suite au deplacement *);;
let buffer_size = 40 ;;
let buffer = String.make buffer_size 'x';;
let nb_read = input ic buffer 0 buffer_size
 (* lecture de maximum buffer_size caracteres dans ic, a stocker dans buffer a partir
     de l'indice 0 *);;
String.sub buffer 0 nb_read;;
close_in ic;;
]]></ocaml-eval>
</subsection>

<subsection id="inout:write" title="Ecriture">
<p>
Voici quelques fonctions d'écriture sur un canal de type <ml>out_channel</ml>.
D'autres fonctions sont disponibles dans la
<ext-a href="&lt;urldocoutputfuns/&gt;">bibliothèque de base</ext-a>.
</p>
<p>
Il est important de savoir que ces canaux sont munis de tampons (buffers). Cela
signifie que l'écriture dans un canal correspondant à un fichier n'écrira pas
forcément immédiatement dans ce fichier, mais attendra qu'une quantité de données
suffisante soit dans le tampon pour procéder à l'écriture réelle, cela afin de
conserver des performances suffisantes lors de multiples petites écritures, sans avoir
à s'en préoccuper. La fermeture d'un canal provoque la vidange du tampon, de même
que la fonction <ml>flush</ml>.
</p>
<ocaml-eval toplevel="true"><![CDATA[
output_string oc second_line (* ecriture d'une chaine sur le canal *);;
flush oc (* forcer l'ecriture du contenu du tampon dans le fichier *);;
output oc buffer 0 nb_read (* ecriture d'un certain nombre de caracteres depuis une chaine *);;
close_out oc;;
]]></ocaml-eval>
</subsection>
</section>

<section id="imper:control" title="Structures de contrôle">
<p>
Nous présentons ci-dessous la séquence d'instructions et les boucles, structures de contrôles
de base pour la programmation impérative. La structure conditionnelle a déjà été présentée
dans le cadre de la programmation fonctionnelle (cf. <elt href="#conditionnelle"/>).
</p>
<subsection id="sequence" title="Séquence">
<p>
La séquence d'instructions est représentée par l'utilisation du '<ml>;</ml>' simple, différent
du '<ml>;;</ml>' de fin de phrase:
</p>
<centerstx><stxid>expression_1</stxid> ; <stxid>expression_2</stxid></centerstx>
<p>
L'<stxid>expression_1</stxid> doit être de type <ml>unit</ml> car sa valeur de retour n'est pas exploitable
dans cette construction. Si ce n'est pas le cas, le compilateur émet un avertissement indiquant
que la valeur retournée par l'expression n'est pas utilisée. Le type de la séquence ainsi que
sa valeur sont ceux de l'<stxid>expression_2</stxid>. Enfin, l'<stxid>expression_1</stxid> est bien sûr
évaluée avant l'<stxid>expression_2</stxid>.
</p>
<ocaml-eval toplevel="true"><![CDATA[
print_endline chaine;
String.sub buffer 0 nb_read;;
1 + 1 ; print_endline chaine;;
]]></ocaml-eval>
<p>
On peut bien sûr composer les séquences:
</p>
<ocaml-eval toplevel="true"><![CDATA[
print_endline chaine ;
print_endline (String.sub buffer 0 10);
print_endline (String.sub buffer 10 20);
1 + 1;;
]]></ocaml-eval>
<p>
La fonction <ml>ignore</ml> permet d'indiquer explicitement qu'une valeur
de retour est volontairement abandonnée:
</p>
<ocaml-eval toplevel="true"><![CDATA[ignore(1+1); 42;;]]></ocaml-eval>
<p>
Si la valeur de la première expression est de type fonctionnel, le compilateur émet un
message d'avertissement différent pour indiquer que la fonction n'a peut-être été que
partiellement appliquée, par exemple si un paramètre a été oublié:
</p>
<ocaml-eval toplevel="true"><![CDATA[let f x y = x + y;;
f 1 ; 42;;
]]></ocaml-eval>
<p>
Attention aux précédences des constructions !
</p>
<ocaml-eval toplevel="true"><![CDATA[
if true then
  print_endline "c'est vrai";
  print_endline "c'est vrai";;
if false then
  print_endline "c'est vrai";
  print_endline "c'est vrai";;
]]></ocaml-eval>
<p>
Que se passe-t-il donc ? En fait, la branche <ml>then</ml> prend fin après le point virgule
situé derrière le premier <ml>print_endline "c'est vrai"</ml>. C'est donc équivalent à
</p>
<ocaml-eval toplevel="true"><![CDATA[
(if false then print_endline "c'est vrai");
print_endline "c'est vrai";;
]]></ocaml-eval>
<p>
Il faut donc utiliser les parenthèses <ml>(</ml> et <ml>)</ml> ou bien les mots-clés
<ml>begin</ml> et <ml>end</ml> pour forcer la séquence à être dans la branche <ml>then</ml>:
</p>
<ocaml-eval toplevel="true"><![CDATA[
if true then
  (
   print_endline "c'est vrai";
   print_endline "c'est vrai"
  );;
if false then
  (
   print_endline "c'est vrai";
   print_endline "c'est vrai"
  );;
]]></ocaml-eval>
</subsection>

<subsection id="loops" title="Boucles">
<p><cmp>Les structures de contrôle itératives sont elles aussi en dehors du monde
fonctionnel. La condition de répétition, ou de sortie, d'une boucle n'a de sens
que si une modification physique de la mémoire permet d'en changer la valeur.
Il existe deux structures de contrôle itératives: la boucle <em>for</em> pour une
itération bornée et la boucle <em>while</em> pour une itération non bornée.
Les structures de boucle elles-mêmes sont des expressions du langage.
Elles retournent donc une valeur: la constante <ml>()</ml> du type <ml>unit</ml>.</cmp>
</p>

<subsubsection id="loopfor" title="Boucle for">
<p>
La boucle for peut être croissante:
</p>
<centerstx>for <stxid>identifiant</stxid> = <stxid>expression_1</stxid> to <stxid>expression_2</stxid> do
  <stxid>expression_3</stxid>
done</centerstx>
<p>
ou décroissante:
</p>
<centerstx>for <stxid>identifiant</stxid> = <stxid>expression_1</stxid> downto <stxid>expression_2</stxid> do
  <stxid>expression_3</stxid>
done</centerstx>
<p>
La valeur de l'identifiant croît ou décroît toujours d'un pas de un.
L'<stxid>identifiant</stxid> n'est visible que dans l'<stxid>expression_3</stxid>
(et masque éventuellement une variable de même nom présente dans l'environnement).
<stxid>expression_1</stxid> et <stxid>expression_2</stxid> doivent être de type <ml>int</ml>.
L'<stxid>expression_3</stxid> doit être de type <ml>unit</ml> car sa valeur est ignorée et l'expression
dans la boucle n'a de sens que si elle a un effet de bord. Si l'expression n'est pas de type
<ml>unit</ml>, le compilateur émet un avertissement.
</p>
<ocaml-eval toplevel="true"><![CDATA[
for i = 10 downto 1 do print_int i; print_string " " done; flush stdout;;
]]></ocaml-eval>
</subsubsection>

<subsubsection id="loopwhile" title="Boucle while">
<p>
La syntaxe de cette boucle est la suivante:
</p>
<centerstx>while <stxid>expression_1</stxid> do <stxid>expression_2</stxid> done</centerstx>
<p>
L'<stxid>expression_1</stxid> doit être de type <ml>bool</ml>. Elle est évaluée au début de chaque tour
de boucle. Si elle est vraie, l'<stxid>expression_2</stxid> est évaluée, sinon la boucle se termine
en une valeur de type <ml>unit</ml>.
Comme pour le corps de la boucle <ml>for</ml>, l'<stxid>expression_2</stxid> doit être de type
<ml>unit</ml>, sinon le compilateur émet un avertissement.
</p>
<ocaml-eval toplevel="true"><![CDATA[
let compteur = ref 1;;
while !compteur <= 10 do
  print_int !compteur; print_string " ";
  incr compteur (* equivalent à compteur := !compteur + 1 *)
done;;
flush stdout;;
]]></ocaml-eval>
</subsubsection>
</subsection>
</section>
<section id="imper:exos" title="Exercices">
<exercice id="printargs" title="Echo">
<p>
Ecrire un programme qui affiche ses arguments, à la manière de la commande UNIX <icode>echo</icode>.
Pour cela, on utilisera <ml>Sys.argv</ml> qui est le tableau des arguments du programme,
ainsi que le module <moduledoc name="Array"/> pour les accès aux tableaux. Le parcours du tableau
sera réalisé à l'aide d'une boucle.
On compilera le programme grâce à la commande
</p>
<sh>ocamlc -o myecho myecho.ml</sh>
<solution>
<ocaml-eval><![CDATA[
for i = 1 to Array.length Sys.argv - 1 do
  print_string Sys.argv.(i); print_string " "
done;;
print_newline();;
]]></ocaml-eval>
</solution>
</exercice>

<exercice id="replace" title="Remplacement de caractère">
<p>
En utilisant une boucle, écrire une fonction <ml>blank</ml> qui prend une chaîne de
caractères, la copie et remplace chaque caractère par un blanc, tant qu'elle
n'a pas rencontré la lettre 's'.
</p>
<solution>
<ocaml-eval toplevel="true"><![CDATA[
let blank str =
  let str = String.copy str in
  let i = ref 0 in
  let len = String.length str in
  while !i < len do
    if str.[!i] = 's' then
      i := len
    else
     ( str.[!i] <- ' ' ; incr i )
  done;
  str
;;
]]></ocaml-eval>
</solution>
<p>
Tester ensuite la fonction, par exemple:
</p>
<ocaml-eval toplevel="true"><![CDATA[let str = "coucous_royal";;
blank str;;
]]></ocaml-eval>
</exercice>

<exercice id="matricesym" title="Test de symétrie de matrice">
<p>
On souhaite écrire une fonction prenant en paramètre une matrice de type
<ml>'a array array</ml> et retournant <ml>true</ml> ou <ml>false</ml> selon que la matrice
est symétrique ou non. On suppose que la matrice est carrée. On utilisera les matrices
<ml>mat1</ml> et <ml>mat2</ml> définies ci-dessous pour tester:
</p>
<ocaml-eval toplevel="true"><![CDATA[
(* matrice symétrique: *)
let mat1 = [| [| 0 ; 1 ; 2 |] ; [| 1 ; 5 ; 10 |] ; [| 2 ; 10 ; 8 |] |];;
let init _ = Random.int 10 ;;
(* matrice non symétrique *)
let mat2 = [| Array.init 3 init ; Array.init 3 init ; Array.init 3 init |];;
mat2.(0).(1) <- - mat2.(1).(0);;
]]></ocaml-eval>
<p>
On commence par écrire une fonction <ml>is_sym_imper</ml> impérative.
</p>
<solution>
<ocaml-eval toplevel="true"><![CDATA[
let is_sym_imper mat =
  let len = Array.length mat in
  let res = ref true in
  for i = 0 to len - 1 do
    for j = i + 1 to len - 1 do
      if mat.(i).(j) <> mat.(j).(i) then res := false
    done
  done;
  !res
;;
is_sym_imper mat1;;
is_sym_imper mat2;;
]]></ocaml-eval>
</solution>
<p>
On donnera ensuite une version fonctionnelle <ml>is_sym_fun</ml>.
</p>
<solution>
<ocaml-eval toplevel="true"><![CDATA[
let is_sym_fun mat =
  let len = Array.length mat in
  let rec iterj i j =
    (j > len - 1) ||
    (mat.(i).(j) = mat.(j).(i) && iterj i (j+1))
  in
  let rec iteri i =
    (i > len - 1) ||
    (iterj i (i+1) && iteri (i+1))
  in
  iteri 0
;;
is_sym_fun mat1;;
is_sym_fun mat2;;
]]></ocaml-eval>
</solution>
</exercice>
</section>
</mychapter>