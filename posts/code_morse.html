<post title="Code Morse"
section-counter="false"
date="2013/01/23"
keywords="string, map, dictionnaire, codage, decodage, morse"
topics="filtrage, définition de fonction, programme"
>
<p>Nous allons réaliser un programme simple effectuant
la transcription depuis et vers du code Morse.
</p>
<sep_/>
<section id="morsecode" title="Le code Morse">
<p>
Le code Morse est un encodage de caractères alphanumériques utilisant
des impulsions sonores courtes et longues. Les impulsions courtes
seront représentées par le caractère <icode>.</icode> (point) et
les longues par le caractère <icode>_</icode> (trait bas).
Les codes de deux lettres d'un mot sont séparées par un espace, et les mots
par deux espaces.
</p>
<p>
Nous souhaitons que notre programme prenne en argument
la chaîne à encoder ou décoder, ainsi qu'une option indiquant s'il
s'agit de coder ou décoder. Ensuite, nous procéderons comme suit:
</p>
<ul>
<li>Pour le codage d'un texte vers du code Morse:
Pour chaque caractère, appliquer un mapping vers le code Morse
  associé. Si ce n'est pas le dernier caractère, on ajoute un espace
  en plus.
</li>
<li>Pour le décodage depuis du code Morse:
On découpe la chaîne selon les espaces. On applique un mapping
de chaque "mot" vers la lettre ou le chiffre correspondant. La chaîne
vide (issue de deux espaces consécutifs) est transformée en un
espace, séparateur de mot.
</li>
</ul>
</section>
<section id="mapping" title="Mapping">
<p>
La première chose à faire est d'implémenter la correspondance
entre lettre ou chiffre et code Morse. Pour cela, nous définissons
une liste de ces correspondances, qui est une liste de paires
(caractère, chaîne):
</p>
<ocaml-eval toplevel="true">
let mapping =
  [ 'A', "._" ; 'B', "_..." ; 'C', "_._." ;
    'D', "_.." ; 'E', "." ; 'F', ".._." ;
    'G', "__." ; 'H', "...." ; 'I', ".." ;
    'J', ".___" ; 'K', "_._" ; 'L', "._.." ;
    'M', "__" ; 'N', "_." ; 'O', "___" ;
    'P', ".__." ; 'Q', "__._" ; 'R', "._." ;
    'S', "..." ; 'T', "_" ; 'U', ".._" ;
    'V', "..._" ; 'W', ".__" ; 'X', "_.._" ;
    'Y', "_.__" ; 'Z', "__.." ; '0', "_____" ;
    '1', ".____" ; '2', "..___" ; '3', "...__" ;
    '4', "...._" ; '5', "....." ; '6', "_...." ;
    '7', "__..." ; '8', "___.." ; '9', "____." ;
  ] ;;
</ocaml-eval>
</section>
<section id="encoding" title="Encodage">
<p>
Pour l'encodage, il nous faut définir une fonction prenant
un caractère et retournant la chaîne du code Morse associé.
</p>
<p>
Une première possibilité serait d'utiliser la fonction <ml>List.assoc</ml>
qui parcourerait le mapping à la recherche de la chaîne associée à
un caractère en paramètre. Cependant, ce n'est pas très efficace, car
cela nécessite de parcourir souvent une bonne partie de la liste.
On pourrait mettre en tête de la liste les caractères les plus utilisés,
mais nous préférons utiliser une dictionnaire, grâce au module
<moduledoc name="Map"/> de la bibliothèque standard d'OCaml.
</p>
<p>
Pour créer notre dictionnaire, nous définissons un module permettant
d'utiliser les caractères comme clé de notre dictionnaire. Il doit
avoir la signature <moduledoc name="Map.OrderedType"/>, c'est-à-dire
que c'est un module définissant un type <ml>t</ml> et une fonction
de comparaison sur ce type, appelée <ml>compare</ml>. Nous définissons
donc simplement ce module en disant que le type <ml>t</ml> est le
type <ml>char</ml> et la fonction de comparaison est <ml>Pervasives.compare</ml>,
fonction polymorphe du système permettant la comparaison sur n'importe quelle
structure de données:
</p>
<ocaml-eval toplevel="true">
module Char_key = struct type t = char let compare = Pervasives.compare end;;
</ocaml-eval>
<p>
Il ne nous reste plus qu'à appliquer le foncteur <moduledoc name="Map.Make"/>
sur notre module définissant le type de la clé pour obtenir un module
permettant de manipuler des dictionnaires avec des caractères comme clés:
</p>
<ocaml-eval>
module Char_map = Map.Make (Char_key);;
</ocaml-eval>
<p>
L'étape suivante consiste à créer un dictionnaire associant
les caractères et leur représentation en code Morse. Pour cela,
nous allons itérer sur la liste de correspondances, en remplissant un
dictionnaire, du type <ml>Char_map.t</ml>. Le dictionnaire initial
utilisé lors de l'itération est le dictionnaire vide <ml>Char_map.empty</ml>:
</p>
<ocaml-eval toplevel="true">
let map_to_morse = List.fold_left
  (fun map (char, morse_code) -> Char_map.add char morse_code map)
  Char_map.empty mapping;;
</ocaml-eval>
<p>On se référera à la documentation de la fonction <ml>List.fold_left</ml>
si on ne la connaît pas.
</p>
<p>
Avec ce dictionnaire, nous pouvons maintenant facilement définir
une fonction prenant en paramètre un caractère et renvoyant son
code Morse ou, si le caractère est invalide, levant
l'exception <ml>Invalid_argument</ml> avec le caractère en question.
Nous ajoutons un traitement spécial pour gérer le caractère d'espace,
séparateur de mot: dans ce cas, la fonction renvoie une chaîne
vide; un espace supplémentaire sera rajouté si ce n'est pas le
dernier caractère, ce qui permettra bien d'avoir deux caractères
pour séparer les mots (voir plus bas):
</p>
<ocaml-eval toplevel="true">
let char_to_morse = function
  ' ' -> ""
| c ->
  try Char_map.find c map_to_morse
  with Not_found ->
     let msg = Printf.sprintf "char_to_morse: invalid character %C" c in
    raise (Invalid_argument msg)
;;
</ocaml-eval>
<p>
Pour encoder un message complet, il nous reste à définir une fonction
procédant de la façon suivante:
</p>
<ul>
<li>Création d'un buffer pour ajouter des chaînes,</li>
<li>Parcours du texte à encoder: pour chaque caractère, on ajoute
  au buffer le code correspondant, en utilisant notre fonction <ml>char_to_morse</ml>;
  on ajoute un espace si ce n'est pas le dernier caractère,</li>
<li>On retourne le contenu du buffer.</li>
</ul>
<ocaml-eval toplevel="true"><![CDATA[
let to_morse str =
  let len = String.length str in
  let buf = Buffer.create 256 in
  let on_char i c =
    Buffer.add_string buf (char_to_morse c);
    if i < len - 1 then Buffer.add_char buf ' '
  in
  String.iteri on_char str;
  Buffer.contents buf
;;
(* Pour tester *)
let morse_formation_ocaml = to_morse "FORMATION OCAML";;
]]></ocaml-eval>
</section>
<section id="decoding" title="Décodage">
<p>
Pour le décodage, nous définissons également un dictionnaire
à partir de la liste de correspondances. Cette fois la clé est
une chaîne (le code Morse). A titre d'exemple, nous utilisons
un module anonyme pour créer notre module de dictionnaire:
</p>
<ocaml-eval>
module Str_map =
  Map.Make (struct type t = string let compare = Pervasives.compare end);;
</ocaml-eval>
<ocaml-eval toplevel="true">
let map_from_morse = List.fold_left
  (fun map (char, morse) -> Str_map.add morse char map)
  Str_map.empty mapping ;;
</ocaml-eval>
<p>
La fonction de décodage d'une chaîne ressemble à celle d'encodage:
</p>
<ocaml-eval toplevel="true">
let char_from_morse = function
  "" -> ' '
| str ->
   try Str_map.find str map_from_morse
   with Not_found ->
     let msg = Printf.sprintf "char_from_morse: invalid Morse code %S" str in
     raise (Invalid_argument msg)
;;
</ocaml-eval>
<p>
Pour itérer sur les codes Morse d'une chaîne, nous devons d'abord découper
cette chaîne en codes, en utilisant l'espace comme séparateur.
Pour cela, nous définissons une fonction découpant une chaîne en mots, et
gardant les mots vides quand il y a deux espaces. Cela nous permettra
de savoir quand insérer un espace supplémentaire pour séparer des mots
dans la chaîne décodée:
</p>
<ocaml-eval toplevel="true">
let split_string s =
  let len = String.length s in
  let rec iter acc pos =
    if pos >= len then
      match acc with
        "" -> []
      | _ -> [acc]
    else
      match s.[pos] with
       ' ' -> acc :: (iter "" (pos + 1))
      | c -> iter (Printf.sprintf "%s%c" acc c) (pos + 1)
  in
  iter "" 0
;;
(* pour tester *)
split_string "maître corbeau sur un arbre  perché";;
</ocaml-eval>
<p>
Il ne nous reste plus qu'à définir la fonction décodant un
texte en Morse:
</p>
<ocaml-eval toplevel="true"><![CDATA[
let from_morse str =
  let words = split_string str in
  let buf = Buffer.create 256 in
  let on_word i str = Buffer.add_char buf (char_from_morse str) in
  List.iteri on_word words;
  Buffer.contents buf
;;
(* Pour test *)
from_morse morse_formation_ocaml;;
]]></ocaml-eval>
</section>
<section id="program" title="Le programme">
<p>
Pour terminer notre programme, il ne nous reste qu'à
implémenter la gestion de la ligne de commande:
une option <icode>-d</icode> pour demander le décodage au
lieu du codage, et concaténer les autres chaînes passées en paramètre.
On pourra se référer à la documentation du module <moduledoc name="Arg"/>.
</p>
<ocaml-eval toplevel="true"><![CDATA[
let main () =
  let decode = ref false in
  let options = [ "-d", Arg.Set decode, " decode Morse code" ] in
  let args = ref [] in
  Arg.parse options
    (fun arg -> args := arg :: !args)
    (Printf.sprintf "Usage: %s [-d] <text|Morse>" Sys.argv.(0));
  let text = String.concat " " (List.rev !args) in
  let result = (if !decode then from_morse else to_morse) text in
  print_endline result
;;
]]></ocaml-eval>
<ocaml>
try main ()
with Invalid_argument msg ->
  prerr_endline msg;
  exit 1
;;
</ocaml>
<p>
Si notre code se trouve dans le fichier <icode>code_morse.ml</icode>,
nous pourons le compiler avec la commande suivante:
</p>
<command-line>$ ocamlopt -o code_morse code_morse.ml</command-line>
<p>
Et nous pouvons tester le résultat:
</p>
<command-line><![CDATA[
$ ./code_morse --help
Usage: ./code_morse [-d] <text|Morse>
  -d  decode morse code
  -help  Display this list of options
  --help  Display this list of options
$ ./code_morse "C EST LA FORMATION OCAML"
_._.  . ... _  ._.. ._  .._. ___ ._. __ ._ _ .. ___ _.  ___ _._. ._ __ ._..
$ ./code_morse -d "_._.  . ... _  ._.. ._  .._. ___ ._. __ ._ _ .. ___ _.  ___ _._. ._ __ ._.."
C EST LA FORMATION OCAML
$ ./code_morse "erreur ce sont des minuscules"
char_to_morse: invalid character 'e'
$ ./code_morse -d "____...___" # code inconnu
char_from_morse: invalid Morse code "____...___"
]]></command-line>
</section>
</post>